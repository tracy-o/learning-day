#!/usr/bin/env groovy

library 'BBCNews'

def result = 'PASSED'
def exception

def cronlist = BRANCH_NAME == 'master' ? '''
30 09 * * 1-5 % ENVIRONMENT=test;
00 16 * * 1-5 % ENVIRONMENT=test;
''' : ''

node {
  cleanWs()
  checkout scm

  properties([
    buildDiscarder(logRotator(numToKeepStr: '5', daysToKeepStr: '2', artifactNumToKeepStr: '5', artifactDaysToKeepStr: '2')),
    disableConcurrentBuilds(),
    pipelineTriggers([parameterizedCron(cronlist)]),
    parameters([
        choice(choices: ['test'], description: 'The environment to run the tests against', name: 'ENVIRONMENT'),
        choice(choices: ['all', 'belfrage', 'cedric-belfrage', 'bruce-belfrage'], description: 'The target to run the tests against', name: 'TARGET'),
        string(name: 'SLACK_MESSAGE', defaultValue: '', description: 'Optionally enter a value to send the result along with this message to the Belfrage slack channel.'),
        string(name: 'DEPLOYMENT_ID', defaultValue: '', description: 'Optionally enter the cosmos deployment ID to report the status of this smoke test to')
      ])
  ])

  stage('Run Smoke tests') {
    docker.image('qixxit/elixir-centos').inside("-u root") {
      sh 'mix deps.get'

      try {
        ansiColor('xterm') {
          if (params.TARGET == 'all') {
            sh "MIX_ENV=smoke_test mix test --color --trace test/smoke/"
          } else {
            sh "MIX_ENV=smoke_test mix test --color --trace test/smoke/ --only ${params.TARGET}_${params.ENVIRONMENT}:true"
          }
        }
      } catch (err) {
        result = 'FAILED'
        exception = err
      }
    }
  }

  if (result == 'FAILED') {
    stage('Report result to slack') {
      currentBuild.description = "${params.ENVIRONMENT} ${result}"
      slackSend channel: "#team-belfrage", message: "Belfrage Smoke tests ${result}: ${env.RUN_DISPLAY_URL}. ${params.SLACK_MESSAGE}"
    }
  }

  if (params.DEPLOYMENT_ID != '') {
    stage('Tag cosmos deployment') {
      tagType = (result == 'FAILED') ? 'fail' : 'pass'
      BBCNews.labelCosmosDeployment(params.DEPLOYMENT_ID, tagType, 'Belfrage smoke test result', env.RUN_DISPLAY_URL)
    }
  }

  stage("clean up after ourselves") {
    cleanWs()
    dir("${env.WORKSPACE}@libs") {
      deleteDir()
    }
    if (exception != null) {
      throw exception
    }
  }
}
