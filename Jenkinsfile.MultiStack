#!/usr/bin/env groovy

library 'devops-tools-jenkins'

def dockerRegistry = libraryResource('dockerregistry').trim()
def dockerImage = "${dockerRegistry}/bbc-news/elixir-centos7:1.13.1-erlang-24.3.4.2"

library 'BBCNews'

String buildVariables() {
  def envFile = readFile 'build.env'
  def envVars = ''
  envFile.split('\n').each { env ->
    envVars = "$envVars -e $env"
  }
  envVars
}

// This function is adapted from deployReleaseVersion() in:
// https://github.com/bbc/devops-tools-jenkins/blob/646f31354a1c59baedd07384cca5a58fca548892/vars/BBCNews.groovy#L131-L141
// however here we can also speficfy the environment we want to deploy to.
def deployReleaseVersion(String cosmosService, String version, Boolean forceRelease=false, String environment="test") {
    stage('Cosmos deploy') {
        if (env.BRANCH_NAME == 'master' || forceRelease) {
            docker.image('node:14.15').inside("-u root -v /etc/pki/tls:/etc/pki/tls"){
            sh('npm install')
            sh("npm install git+https://${GITHUB_TOKEN}:x-oauth-basic@github.com/bbc/cosmos-deploy.git#v3")
            sh("./node_modules/.bin/cosmos-deploy promote ${cosmosService} ${environment} ${version}")
            }
        }
    }
}

node {
  cleanWs()
  checkout scm

  properties([
    buildDiscarder(logRotator(daysToKeepStr: '7', artifactDaysToKeepStr: '7')),
    parameters([
        choice(choices: ['belfrage', 'belfrage-preview', 'bruce-belfrage', 'cedric-belfrage', 'joan-belfrage', 'sally-belfrage', 'sydney-belfrage'], description: 'The Cosmos Service to create a release for', name: 'SERVICE'),
        booleanParam(defaultValue: false, description: 'Force release from non-master branch', name: 'FORCE_RELEASE'),
        choice(choices: ['test', 'live'], description: 'The highest environment the release will be promoted to', name: 'DEPLOYMENT_ENVIRONMENT')
    ])
  ])

  try {
    stage("Fetch JWK data") {
      jwk_result = sh(script: "python3 ./jwk-fetcher.py test,live", returnStdout:true).trim()

      if ((jwk_result =~ /success/)) {
        println("JWK keys success")
      } else {
        println("JWK error, getting the key data failed")
        error("JWK Error")
      }
    }

    stage('Build RPM') {
      sh "./download_bbc_error_pages.sh"
      sh "cp cosmos_config/${params.SERVICE}.json cosmos/release-configuration.json"
      String vars = buildVariables()
      docker.image(dockerImage).inside("-u root -e MIX_ENV=prod ${vars}") {
        sh 'rpm -i bbc-error-pages.rpm'
        sh 'mix deps.get'
        sh 'mix hex.audit'
        sh 'mix distillery.release'
      }
      sh 'cp _build/prod/rel/belfrage/releases/*/belfrage.tar.gz SOURCES/'
    }

    BBCNews.archiveDirectoryAsPackageSource('bake-scripts', 'bake-scripts.tar.gz')
    String version

    stage('Get release version') {
      Boolean addShaToVersion=true
      version = BBCNews.getNextPackageVersionFromCosmos(params.SERVICE, params.FORCE_RELEASE, env.BRANCH_NAME, false, addShaToVersion)
    }

    BBCNews.buildRPMWithMockAndVersion(params.SERVICE, 'belfrage.spec', version)
    BBCNews.setRepositories(params.SERVICE, 'repositories.json')
    BBCNews.cosmosRelease(params.SERVICE, 'RPMS/*.x86_64.rpm', params.FORCE_RELEASE)
    if (params.DEPLOYMENT_ENVIRONMENT == "live") {
      deployReleaseVersion(params.SERVICE, version, params.FORCE_RELEASE, "test")
      deployReleaseVersion(params.SERVICE, version, params.FORCE_RELEASE, "live")
    } else {
      deployReleaseVersion(params.SERVICE, version, params.FORCE_RELEASE, params.DEPLOYMENT_ENVIRONMENT)
    }
    

  } catch(err) {
    if (env.BRANCH_NAME == "master") {
      stage("Report result to slack") {
        slackSend channel: "#team-belfrage", message: ":fire: Release failed for *${params.SERVICE}* <${env.RUN_DISPLAY_URL}|more info>"
      }
    }
    currentBuild.result = 'FAILURE'
  }

  stage("clean up after ourselves") {
    docker.image(dockerImage).inside("-u root") {
      sh "rm -rf ${env.WORKSPACE}/{deps,_build,local.log}"
    }
    cleanWs()
    dir("${env.WORKSPACE}@libs") {
      deleteDir()
    }
  }
}
